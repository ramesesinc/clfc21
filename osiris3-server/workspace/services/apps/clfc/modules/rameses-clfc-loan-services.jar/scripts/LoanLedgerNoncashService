import com.rameses.common.*;
import com.rameses.annotations.*;
import com.rameses.services.extended.*;
import java.rmi.server.UID;

class LoanLedgerNoncashService extends ActiveCrudListService
{
	@Env
	def env;

	@ActiveDB("loan_ledger_noncash")
	def em;

	@ActiveDB("onlinecollection")
	def onlinecollection;

	@ActiveDB("loan_ledger")
	def ledger;

	@ActiveDB("loanapp")
	def loanapp;

	@ActiveDB("borrower")
	def borrower;

	@Service("DateService")
	def dateSvc;

	@Service("BranchService")
	def branchSvc;

	@Service("SequenceService")
	def seqSvc;

	@Service("LoanLedgerBillingService")
	def billingSvc;

	@ProxyMethod
	public def getColumns( params ) {
		def cols = [];
		if (!params.state) cols << [name: 'txnstate', caption: 'Status'];

		cols << [name: 'txntype', caption: 'Type'];
		cols << [name: 'refno', caption: 'Ref. No.'];
		cols << [name: 'borrower.name', caption: 'Borrower'];
		cols << [name: 'loanapp.appno', caption: 'App. No.'];
		cols << [name: 'txndate', caption: 'Collection Date', type: 'date', outputFormat: 'MMM-dd-yyyy'];
		cols << [name: 'amount', caption: 'Amount', type: 'decimal', format: '#,##0.00'];

		if (params.state == 'COLLECTED') {
			cols << [name: 'dtcollected', caption: 'Date Collected'];
			cols << [name: 'collectedby.name', caption: 'Collected By'];
		}

		return cols;
	}

	void beforeList( params ) {
		params.searchtext = (params.searchtext? params.searchtext : '') + '%';
		if (params.state) {
			params._listMethod = 'getListByState';
		}
	}

	void afterList( params, list ) {
		list.each{ o->
			o.filtype = 'ledgernoncash';
			if (o.txntype == 'CAPTURE') o.filetype = 'captureledgernoncash';
		}
	}

	@ProxyMethod
	public def getStates() {
		return [
			[caption: 'All'],
			[caption: 'DRAFT', state: 'DRAFT'],
			[caption: 'COLLECTED', state: 'COLLECTED'],
			[caption: "VOID PENDING", state: "VOID_PENDING"],
			[caption: "VOIDED", state: "VOIDED"]
		];
	}

	void beforeCreate( data ) {
		data.dtcreated = dateSvc.serverDate;
		data.author = [objid: env.USERID, name: env.NAME];
		data.txntype = 'ONLINE';
		if (!data.txnstate) data.txnstate = 'DRAFT';
	}

	void afterCreate( data ) {
		if (data.txnstate == 'DRAFT' && !data.reftype) {
			data.allowedit = true;
		}
	}

	void afterUpdate( data ) {
		data.allowedit = true;
	}

	void afterOpen( data ) {
		if (data?.txnstate == 'DRAFT' && !data?.reftype) {
			data.allowedit = true;
		}

		def l = ledger.read( [objid: data.parentid] );
		def app = loanapp.read( [objid: l?.appid] );

		data.appno = app?.appno;
	}

	@ProxyMethod
	public def getInfo( params ) {
		if (!params.ledgerid) throw new RuntimeException("Please specify ledgerid.");

		def l = ledger.read( [objid: params.ledgerid] );
		if (!l) throw new RuntimeException("Ledger record does not exist or has already been deleted.");

		def app = loanapp.read( [objid: l.appid] );
		if (!app) throw new RuntimeException("Loan Application record does not exist or has already been deleted.");

		def pb = borrower.read( [objid: app.borrower.objid] );
		if (!pb) throw new RuntimeException("Borrower record does not exist or has already been deleted.");

		def data = [
			loanapp 	: [
				objid 	: app.objid,
				appno 	: app.appno,
				amount 	: app.loanamount
			],
			borrower 	: pb
		]
		return data;
	}

	@ProxyMethod
	public def saveCollectedNoncash( data ) {
		data = create( data );
		data = collect( data );
		
		return open( data );
	}

	@ProxyMethod
	public def collect( params ) {
		def data = em.read(params);
		if (!data) throw new Exception("Ledger non-cash record does not exist or has already been deleted.");

		if (data.txnstate == 'COLLECTED') {
			throw new Exception("Ledger non-cash record has already been collected.");
		}

		data.txnstate = 'COLLECTED';
		em.changeState(data);

		def currdate = dateSvc.getServerDateAsString();
		def collectedby = [objid: env.USERID, name: env.NAME];

		def collector = onlinecollection.read(collectedby, "oc-collector");
		if (!collector) {
			onlinecollection.create(collectedby, "oc-collector");
		}

		def prm = [
			txndate 	: currdate.split(" ")[0], 
			collectorid	: collectedby.objid, 
			state 		: 'DRAFT'
		];

		def col = onlinecollection.findCollectionByDateAndCollectorAndState( prm );
		if (!col || col.state != 'DRAFT') {
			col = [
				objid		: 'OC' + new UID(),
				state		: 'DRAFT',
				txndate 	: currdate,
				dtfiled 	: currdate,
				author		: collectedby,
				collector 	: collectedby,
				remittanceid: "REM" + new UID()
			];
			onlinecollection.create(col);
		}

		def mledger = ledger.read([objid: data.parentid]);
		if (!mledger) throw new Exception("Ledger record does not exist or has already been deleted.");

		def item = em.findByIdWithInfo(data);
		if (!item) throw new Exception("Non-cash record does not exist or has already been deleted.");

		def refno = billingSvc.getRefno([routecode: item?.route?.code]);

		def detail = onlinecollection.findDetailByNoncashidAndCollectionid([noncashid: data.objid, collectionid: col.objid]);
		if (!detail) {
			detail = [
				objid 		: 'OCD' + new UID(),
				parentid 	: col.objid,
				loanapp 	: item?.loanapp,
				borrower 	: item?.borrower,
				route 		: item?.route,
				dtpaid 		: currdate,
				paidby 		: data.paidby,//item?.borrower?.name,
				refno 		: refno,
				paytype 	: item.paymentmethod,
				payoption 	: 'noncash',
				amount 		: data.amount
			];
			onlinecollection.create(detail, "oc-detail");

			def i = [
				objid 		: data.objid,
				collectionid: col.objid,
				refid 		: detail.objid,
				noncashid 	: data.objid
			];
			em.create(i, "collection-noncash");
		}

		data.dtcollected = currdate;
		data.collectedby = collectedby;
		data.refno = refno;
		data.txndate = currdate;
		em.update(data, "noncash-collect");

		return open(data);
	}
}